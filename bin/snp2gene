#!/usr/bin/env nextflow

params.out = '.'

map = file(params.map)
params.v = 28
params.genome = 'GRCh38'

tag1 = (params.genome == 'GRCh37') ? '/GRCh37_mapping' : ''
tag2 = (params.genome == 'GRCh37') ? 'lift37' : ''

process download_hgnc {

    output:
        file 'non_alt_loci_set.txt' into hgnc

    '''
    wget ftp://ftp.ebi.ac.uk/pub/databases/genenames/new/tsv/non_alt_loci_set.txt
    '''
}

process download_annotation {

    input:
        val GENCODE_VERSION from params.v
        val TAG1 from tag1
        val TAG2 from tag2

    output:
        file 'gff3' into gff
    
    script:
    template 'dbs/gencode.sh'

}

process extract_genes {

	input:
		file gff

	output:
		file 'genes.gff' into genes_gff

	"""
	awk '{if(\$3=="gene" || \$3=="pseugene"){print \$0}}' $gff >genes.gff
	"""

}

process gff2bed {

	input:
		file genes_gff

	output:
		file 'genes.bed' into genes_bed

	"""
	gff2bed < $genes_gff >genes.bed
	"""

}

process map2bed {

	input:
		file map

	output:
		file 'snps.bed' into snps_bed

	"""
	awk '{print "chr" \$1 "\\t" \$4 "\\t" \$4 "\\t" \$2 "\\t.\\t." }' $map >tmp
	sed 's/chr23/chrX/' tmp >snps.bed
	"""

}

process snp2gene {

	input:
		file snps_bed
		file genes_bed

	output:
		file 'snp2ensembl.tsv' into snp2ensembl

	"""
	bedtools intersect -a $snps_bed -b $genes_bed -wa -wb >tmp
	cut -f4,16 tmp | sed 's/\\tID=/\\t/' | sed 's/\\.[0-9]\\+;.\\+//' >snp2ensembl.tsv
	"""

}

process ensembl2hgnc {

	publishDir "$params.out", overwrite: true, mode: "copy"

	input:
		file hgnc
		file snp2ensembl

	output:
		file 'snp2hgnc.tsv' into snp2hgnc

	"""
	#!/usr/bin/env Rscript
	library(tidyverse)
	library(magrittr)
	ensembl2hgnc <- read_tsv('$hgnc') %>%
		select(symbol, ensembl_gene_id)
	read_tsv('$snp2ensembl', col_names = FALSE) %>%
		set_colnames(c('snp','ensembl_gene_id')) %>%
		inner_join(ensembl2hgnc, by = 'ensembl_gene_id') %>%
		select(snp, symbol) %>%
		write_tsv('snp2hgnc.tsv')
	"""

}